set term svg font ",16" background rgb "white" size 1200, 960
set out "BesselPlot.svg"
set multiplot layout 2,2
set xlabel "x"
set ylabel "y"
set xrange [0:10]

set title "Subspline on tabulated j_{0}(x) and j_{0}'(x) values"
plot sin(x)/x lw 2 lt rgb "red" title "j_{0}(x)"\
, "splineBessel.txt" using 1:2 with lines dt 2 lw 2 lt rgb "blue" title "Subspline"\
,"tabValBessel.txt" using 1:2 with points ps 0.7 pt 7 lt rgb "black" title "Tabulated j_{0}(x) values"

set title "Subspline 1st derivative"
set key bottom right
plot (x*cos(x)-sin(x))/(x*x) lw 2 lt rgb "red" title "j_{0}'(x)"\
, "splineBessel.txt" using 1:3 with lines lw 2 dt 2 lt rgb "blue" title "Subspline 1st deriv."\
,"tabValBessel.txt" using 1:3 with points ps 0.7 pt 7 lt rgb "black" title "Tabulated j'_{0}(x) values"

set title "Subspline integral (from 0 to x)"
set key bottom center
plot "tabValJ0Int.txt" with points ps 0.7 pt 7 lt rgb "red" title "Si(x) tabulated values"\
, "splineBessel.txt" using 1:4 with lines dt 2 lw 2 lt rgb "blue" title "Subspline integral"

set title "Subspline 2nd derivative"
set key bottom right
plot (-1*(x*x-2)*sin(x) - 2*x*cos(x))/(x*x*x) lw 2 lt rgb "red" title "j_{0}''(x)"\
, "splineBessel.txt" using 1:5 with lines lw 2 lt rgb "blue" title "Subspline 2nd deriv."\
