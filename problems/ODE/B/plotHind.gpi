# Small trick to retrieve the x and y-value right before introducing social distancing
# and other start/end x and y-values used for formatting and correctly placing elements
set terminal unknown
plot "out.txt" index 1 using 1:3
xmax = GPVAL_DATA_X_MAX
ymaxI = GPVAL_DATA_Y_MAX
plot "out.txt" index 1 using 1:2
ymaxSA = GPVAL_DATA_Y_MAX
yminSA = GPVAL_DATA_Y_MIN
plot "out.txt" index 2 using 1:2
yminSB = GPVAL_DATA_Y_MIN

# Now do the actual plotting
set term svg dashed size 1280, 480 font",16" background rgb "white"
set out "OutbreakHindered.svg"
set multiplot layout 1,2 title "SIR model of Covid-19 outbreak in Denmark (hindered, T_r/T_c = 2.5 -> T_r/T_c = 0.6)"
set xlabel "Days since return of Ischgl tourists"
set ylabel "People"
set key top left box

set arrow from xmax,0 to xmax,ymaxI nohead dt 2 lt rgb "black"
set label "Introduced social distancing" at xmax+1, 0.2*ymaxI font ",12"
plot "out.txt" index 1 using 1:3 with lines lt rgb "blue" title "Currently infected"\
, "out.txt" index 2 using 1:3 with lines lt rgb "blue" notitle \
, "out.txt" index 1 using 1:4 with lines lt rgb "red" title "Diseased or recovered"\
, "out.txt" index 2 using 1:4 with lines lt rgb "red" notitle

set key top right
set yrange [yminSB:ymaxSA]
set arrow from xmax,yminSA to xmax,yminSB nohead dt 2 lt rgb "black"
set label "Introduced social distancing" at xmax+1, 0.2*(yminSA-yminSB)+yminSB font ",12" offset -14.5,0
plot "out.txt" index 1 using 1:2 with lines lt rgb "green" title "Susceptible"\
, "out.txt" index 2 using 1:2 with lines lt rgb "green" notitle
